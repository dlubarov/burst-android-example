apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.lubarov.daniel.burstandroidexample"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        // Exclude some duplicate files as a workaround for
        // https://code.google.com/p/android/issues/detail?id=61573
        exclude 'LICENSE.txt'
        exclude 'APK LICENSE.txt'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.3'

    androidTestCompile "com.android.support.test:testing-support-lib:0.1"
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.0"
    androidTestCompile "com.squareup.burst:burst-junit4:1.0.2"
}

configurations.all {
    // Espresso depends on the now-obsolete junit-dep module. There's no need for it since we
    // already bring in the main junit module.
    exclude group: 'junit', module: 'junit-dep'

    resolutionStrategy {
        // Espresso depends on version 1.1 of Hamcrest, and doesn't work with 1.3. JUnit relies on
        // hamcrest-core 1.3, but seems to work with 1.1. So we need to override Gradle's default
        // resolution strategy and force 1.1 to be used.
        force 'org.hamcrest:hamcrest-core:1.1'
        force 'org.hamcrest:hamcrest-integration:1.1'
        force 'org.hamcrest:hamcrest-library:1.1'
    }
}